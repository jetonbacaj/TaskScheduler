package taskcoordinator;
dialect  "mvel"

rule "Server currently processing file"
    salience 100
    when
        $server : Server()
        $task : Task()
        $response : BidResponse(task == $task, server == $server, fileAffinity == "yes")
    then
        $task.setRule(drools.getRule().getName());
        modify($task) {
            setServer($server),
            setEta($response.timeUntilStart + $response.timeUntilComplete),
            setWaiting(true)
        }
end

rule "Server with a waiting task for file"
    salience 99
    when
        $server : Server()
        $task : Task()
        $response : BidResponse(task == $task, server == $server, fileAffinity == "waiting")
    then
        $task.setRule(drools.getRule().getName());
        retract($task)
end

rule "Any server without waiting task"
    salience 98
    when
        $server : Server()
        $task : Task()
        $response : BidResponse(task == $task, server == $server, timeUntilStart == 0)
        not BidResponse(task == $task, timeUntilStart == 0, timeUntilComplete < $response.timeUntilComplete)
    then
        $task.setRule(drools.getRule().getName());
        modify($task) {
            setServer($server),
            setEta($response.timeUntilComplete)
        }
end
